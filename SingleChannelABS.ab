' Edited from BB_abs_subtract.  Only converts analyte raw signal to
' absorbance.
'
' Does the PMTabsorbance interactive conversion (code inserted).
' Saves the file with the original name and appends "_abs" to the end.
'
' Caution!  It will overwrite files of the same name.
'
' Clear all traces from memory before starting!!!  Not mandatory, but a good idea.
'
' 21.02.2014 ECN  original writing

free

dim linename (40)
dim filename(256)
dim newfile (256)
dim dir(256)
dim prompt (512)
dim auditcomment(1000)
dim txtpoints (256)
dim txtrangeaverage(256)
dim txtrangersd(256)
dim type(25)

onpaint 0	'prettier drawing

'make sure that we load into slots 1 and 2
	files = getsfile()		'are there any files loaded?
	if files = 0 then goto 500	'if no, then go ahead
	setfile 1			'if yes, go to first slot
'to the good stuff

500
	'get base filenames to make file selection easier
	dialogbeg "Base names for data files"
	dialogask linename, 2, 32, 32, "Type the base filename for all line files: "
'	dialogloc 0, 10 : dialogtxt 0, 0, 0, ""
'	dialogask bkgname, 2, 32, 32, "Type the base filename for all background files: "
'	dialogloc 0, 10 : dialogtxt 0, 0, 0, ""
'	dialogask basename, 2, 32, 32, "Type the base filename for all baseline files: "
'	dialogloc 0, 10 : dialogtxt 0, 0, 0, ""
	dialogend 5
	
	string linename = "?*" +$linename + "*.spc"
'	string bkgname = "?*" +$bkgname + "*.spc"
'	string basename = "?*" +$basename + "*.spc"

	string prompt = "Select line file:  " + $linename
'	string prompt(1) = "Select background file:  " + $bkgname
'	string prompt(2) = "Select blackbody trace to subtract:  " +$basename



1000
	'load line trace as f=0, becomes slot 1
	'background as f=1, becomes slot 2
	'!! string index starts at 0, slot index at 1
	'!! we need to use f+1 for the setfile statements


	menufile $filename, $prompt
	loadspc $filename			'open file
	autoscale : see
	strname 3, $newfile = $filename		'get output filename base only, no extension
	


	strname 2, dir = $filename		'gets directory of line file	
	string dir, setdir			'sets current directory (should be set by the open
						'command but this makes it explict)
	

4000
	'Zoom first

	setleft  getffp(), getflp()
	autoscale : see

4020
	dialogbeg "Zoom for processing"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Zoom on the baseline region just before atomization."
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Drag a box (with left mouse button) to zoom"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Hint: include the atomization spike in the box"
	dialogask  mc,4,4050,0,"Do_it"
	dialogend 5

4050
	pointer llx, lly, lx, rx, urx, ury		'lx and ry are ignored and zero,
							'llx and lly are lower-left corner of box
							'urx and ury are upper-right corner of box

	if llx = urx OR lly = ury OR llx = 0 then goto 20000	'trap for box with no area

	setright urx : setleft llx			'set x-axis left and right
	autoscale : see					'set y-axis


	'Absorbance conversion
	'operate on both traces in sequence


	gosub 4100	'start absorbance conversion

	string $type = "_abs"	'set file suffix
	gosub 9200		'audit log
	gosub 8000		'write files
	goto 60000		'GO TO END, SKIP THE SUBTRACTION!!!


	'go do the absorbance calculation

4100
	'this section is a stand-alone method to get the 
	'average of a range of trace values

	pointer cx, cy, lx, rx		'cx and cy are ignored, lx and rx are
					'left and right range x-values

	if npts(#s(lx,rx)) <= 5 then goto 21000
 
	rangeaverage = sum(#s(lx,rx)) / npts(#s(lx,rx))
	DegFre = npts(#s(lx,rx)) -1	'degrees of freedom for std.dev.
	rangersd = ((sqrt(sumd2((#s(lx,rx)),rangeaverage)/DegFre))/rangeaverage)*100

	string txtpoints = "Number of points in range = " + npts(#s(lx,rx))
	string txtrangeaverage = "Average of selected range = " + rangeaverage
	string txtrangersd = "RSD (%) of range = " + rangersd

	dialogbeg "Average value of selected range"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, $txtpoints
	dialogloc 0, 10 : dialogtxt 0, 0, 0, $txtrangeaverage
	dialogloc 0, 10 : dialogtxt 0, 0, 0, $txtrangersd
	dialogask  mc,4, 4500,4100,"Accept Try_again"
	dialogend 5

4500
	'calculate absorbance using selected baseline average
	baseline = rangeaverage			'convert name from above

	#s = -log(#s/baseline)			'calculates absorbance
	setytype 2				'sets y-axis to absorbance
	autoscale
	see

	return




8000
	'file saving

	string newfile = $newfile + $type		'append suffix onto name

	string newfile, setsname	'set the slot name to the appended name
	string newfile = $dir + $newfile
	savespc	$newfile	'saves #s with its slot name, takes care of spc extension automatically

	return



9200
	'auditing for absorbance conversion

	see
	portout addmod, 25	'add conversion to audit log
	'add filenames to audit log	
	string auditcomment = "Original File = " + $newfile
	dblog #0,-3,$auditcomment
	string auditcomment = "Operation = convert to absorbance"
	dblog #0,-3,$auditcomment
	dblog #0,-3,"  "
'	next f
	return




	
'%%  Error Handling %%

20000	'zoom box errors
	dialogbeg "Zoom selection"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "No area selected."
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Select a box containing the area of interest"
	dialogask  mc,4,4020,0,"Try_again"
	dialogend 5

21000	'baseline selection errors
	dialogbeg "Baseline selection"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Not enough points selected."
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Baseline must contain more than 5 points"
	dialogask  mc,4,4100,0,"Try_again"
	dialogend 5


' *******************
' Exit clean up code
' *******************
60000
	dialogbeg "Continue processing"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Process another set of data?"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, "Previous data will be removed from the screen"
	dialogloc 0, 10 : dialogtxt 0, 0, 0, ""
	dialogask mc,4, 61000, 62000, "Yes No"
	dialogend
61000
	noshow

	see
	goto 1000
62000
	free
	end 

