' Convert Textfiles to SPC
' Deals with the lack of compatible text importers in older GRAMS versions
' 12/04/2022 ECN
' 
' Skips header lines that begin with # 
' This was written particularly to convert files created on the ETA Python 
'  software.  Python takes as a standard using # as comment for headers.
' A complete version of this import would look for ASCII characters 48 to 57 (numeric 0 to 9)
'  and reject any initial lines that have non-numeric values.  That is feasible but will 
'  require a longer IF statement.

free

dim filenames(256)
dim getname(1000,256)
dim newfile (1000,256)
dim scratchx(12000)	'maximum file size is 2 minutes at 10 ms per point
dim scratchy(12000)
dim spcname (256)
dim dir(256)
dim junk(256)

1000	ptr = 0
	menufile $filenames="Select files: *.txt",2
	string getname(ptr) = $filenames	'reads first name in list
	strname 2, dir = $filenames		'gets directory of files
1010	onerror 1500				'leaves loop when runs out of filenames
	ptr = ptr + 1
	filenames = 0
	menufile $filenames="",3		'reads next name
	string getname(ptr)=$filenames		'write filename into array of names
	goto 1010				'loop here
	
1500	'status dialog box
	dialogbeg "Processing status"
	dialogloc 1,5,10 : print "Hold on..."
	dialogend 20	'leave dialog visible while processing


2000	for f = 0 to (ptr-1)			'f is the number of files index
	open #1, $getname(f)			'open input file
	strname 3, $newfile(f) = $getname(f)	'get output filename
	string newfile(f) = $dir + $newfile(f)	'put full path into file string
	skip = 0
		for n = 0 to 19
		input #1, "dummy", $junk	'read first 20 lines to find header
		if not(index0(junk - 35) = 0) then goto 2400    'exit on first line NOT starting with #
		skip = skip + 1    'count number of header lines
   		next n
	
' Try reading first 20 lines and have it exit when it finds the first non-# line
'  ascii code 35 is #

2400	close #1   'close and reopen to reset input line to top of file
	open #1, $getname(f)
	i = 0		'initializes data array index
	if skip = 0 then goto 2500  'no header so go directly to read data
	for n = 0 to (skip-1)   'skip the determined number of comment lines
	input #1, "dummy", $junk
	next n

2500	onerror 5000	'catches the end of file and sends it to write
	input #1, "dummy", elapsedtime, value
	scratchx(i) = elapsedtime
	scratchy(i) = value
	i = i + 1
	goto 2500

2900	close #1			'close file reading
	dim xdata(i), ydata(i)		'make data arrays correct size
	xdata = scratchx		'swap scratch to data array
	ydata = scratchy

3000	'file conversion
	newspc zzz(i)
	xadjust #s=xdata,ydata		'write evenly spaced array
	see
	setxtype 4			'x axis in seconds
	setytype 2			'y axis in absorbance
	savespc $newfile(f)
	noshow			'closes new evenly-spaced trace

	free xdata, ydata	
	next f

dialogend -20	'close dialog at end of processing

' *******************
' Exit clean up code
' *******************
4000
	free 
	free *
	end 

' *** Error handling ***
5000	onerror -1:goto 2900





